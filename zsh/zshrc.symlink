# shortcut to this dotfiles path is $ZSH
export ZSH=$HOME/.dotfiles

# your project folder that we can `c [tab]` to
export PROJECTS=~/git


function sourcee() {
  #echo "Sourcing $1"
  source $1
}
# use .localrc for SUPER SECRET CRAP that you don't
# want in your public, versioned repo.
if [[ -a ~/.localrc ]]
then
  sourcee ~/.localrc
fi

# all of our zsh files
typeset -U config_files
config_files=($ZSH/**/*.zsh)


if [[ `uname -s` == "Darwin" ]]; then
  # OSX, load all .zsh files
  # load the path files
  for file in ${(M)config_files:#*/path.zsh}
  do
    sourcee $file
  done

  # load everything but the path and completion files
  for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
  do
    sourcee $file
  done

  # initialize autocomplete here, otherwise functions won't be loaded
  autoload -U compinit
  compinit

  # load every completion after autocomplete loads
  for file in ${(M)config_files:#*/completion.zsh}
  do
    sourcee $file
  done
else
  # Not OS X. Don't source osx.zsh files
  # load the path files
  for file in ${${(M)config_files:#*/path.zsh}:#*/*osx.zsh}
  do
    sourcee $file
  done

  # load everything but the path and completion files
  for file in ${${${config_files:#*/path.zsh}:#*/completion.zsh}:#*/*osx.zsh}
  do
    sourcee $file
  done

  # initialize autocomplete here, otherwise functions won't be loaded
  autoload -U compinit
  compinit

  # load every completion after autocomplete loads
  for file in ${${(M)config_files:#*/completion.zsh}:#*/*osx.zsh}
  do
    sourcee $file
  done

fi

unset config_files
## Completions
autoload -U compinit
compinit -C

## case-insensitive (all),partial-word and then substring completion
zstyle ':completion:*' matcher-list 'm:{a-zA-Z}={A-Za-z}' \
    'r:|[._-]=* r:|=*' 'l:|=* r:|=*'

